plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

kapt {
    correctErrorTypes = true
}


def keystorePropertiesFile = rootProject.file(".gradle/signing.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.evangers.rapidthemore"
        minSdk 16
        targetSdk 31
        versionCode 3
        versionName "1.0.1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs { //keystore 정보
        release {
            storeFile new File(System.properties['user.home'], keystoreProperties['RELEASE_STORE_FILE'])
            storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
            keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
            keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'app'

    productFlavors {
        production {
            dimension 'app'
        }
        qa {
            dimension 'app'
            applicationIdSuffix '.test'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    setProperty("archivesBaseName", "v${defaultConfig.versionName}(${defaultConfig.versionCode})")
}

dependencies {


    implementation 'com.google.android.material:material:1.5.0'

    implementation(
            "androidx.core:core-ktx:$core_version",
            "androidx.navigation:navigation-fragment-ktx:$nav_version",
            "androidx.navigation:navigation-ui-ktx:$nav_version",
            "androidx.fragment:fragment-ktx:$fragment_version",
            "androidx.activity:activity-ktx:$activity_version",
            "androidx.appcompat:appcompat-resources:$appcompat_version",
            "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version",
            "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version",
            "androidx.appcompat:appcompat:$appcompat_version",
            "androidx.appcompat:appcompat-resources:$appcompat_version",
            "androidx.constraintlayout:constraintlayout:$constraint_version"
    )

    implementation("com.google.dagger:hilt-android:2.38.1")
    kapt("com.google.dagger:hilt-android-compiler:2.38.1")

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"

    implementation 'com.google.android.gms:play-services-ads:20.5.0' // admob
    implementation project(':nativetemplates') // admob

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}