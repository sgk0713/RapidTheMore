plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.0.21'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services' // firebase
    id 'com.google.firebase.crashlytics' // crashlytics

}

kapt {
    correctErrorTypes = true
}


def keystorePropertiesFile = rootProject.file(".gradle/signing.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 36

    defaultConfig {
        applicationId "kr.evangers.rapidthemore"
        minSdk 23
        targetSdk 36
        versionCode 14
        versionName "1.6.0.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs { //keystore 정보
        release {
            storeFile new File(System.properties['user.home'], keystoreProperties['RELEASE_STORE_FILE'])
            storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
            keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
            keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'app'

    productFlavors {
        production {
            dimension 'app'
        }
        qa {
            dimension 'app'
            applicationIdSuffix '.test'
        }
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_21.toString()
    }
    setProperty("archivesBaseName", "v${defaultConfig.versionName}(${defaultConfig.versionCode})")
    namespace 'kr.evangers.rapidthemore'
}

dependencies {


    implementation 'com.google.android.material:material:1.12.0'

    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.navigation:navigation-fragment-ktx:2.7.7"
    implementation "androidx.navigation:navigation-ui-ktx:2.7.7"
    implementation "androidx.fragment:fragment-ktx:1.8.2"
    implementation "androidx.activity:activity-ktx:1.9.1"
    implementation "androidx.appcompat:appcompat-resources:1.7.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.4"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.8.4"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.appcompat:appcompat-resources:1.7.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"

    // Compose
    implementation platform("androidx.compose:compose-bom:2024.12.01")
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.activity:activity-compose:1.9.3"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.8.7"
    implementation "androidx.hilt:hilt-navigation-compose:1.2.0"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.34.0"

    debugImplementation "androidx.compose.ui:ui-tooling"

    implementation('com.google.dagger:hilt-android:2.51.1')
    kapt('com.google.dagger:hilt-android-compiler:2.51.1')

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    implementation 'com.google.android.gms:play-services-ads:23.2.0' // admob
    implementation project(':nativetemplates') // admob
    implementation 'com.coupang:ads:1.3.0'

    implementation platform('com.google.firebase:firebase-bom:33.1.2')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

task buildReleaseSetup {
    dependsOn 'bundleProductionRelease'
}